#!/usr/bin/awk -f

BEGIN {
	FS=" "
	if (length(ENVIRON["IRC_TERMTITLE"]))
		printf "\033]0;%s\a", ENVIRON["IRC_TERMTITLE"]

	colors["-"]=length(ENVIRON["IRC_SERVERLOG"]) ? "0" : "38;5;8"

	for (i=32; i < 127; i++)
		char_to_num[sprintf("%c", i)]=i
} {
	if (NF < 2) {
		printf "\n%s", $0
		next
	}

	# Split date into date[1] and time into date[2].
	split(strftime("%a %b %d %Y_%H:%M", $1), date, "_")

	# Display date only when it actually changed.
	if (lastdate != date[1]) {
		lastdate = date[1]
		printf "%s\033[37m-- %s --\033[0m",
			NR != 1 ? "\n\n" : "", date[1]
	}

	nickend=index($2, ">")
	type=substr($2, nickend+1)
	msg=substr($0, index($0, ">") + 2 + length(type))
	nick=substr($2, 2, nickend-2)

	# Set the date color for highlights.
	if (index(type, "!")) {
		printf "\a"
		dcolor="31"
	} else {
		dcolor="38;5;8"
	}

	# Status message.
	if (nick == "-") {
		printf "\n\033[%sm%s \033[%sm%s\033[0m",
			dcolor, date[2], colors["-"], msg
		next
	}

	# My message.
	if (index(type, "*")) {
		ncolor="0"

	# Other message.
	} else {
		# Calculate nick color if necessary.
		if ((ncolor=colors[nick]) == "") {
			i=1
			sum=0
			while (char=substr(nick, i++, 1))
				sum=sum + char_to_num[char]

			ncolor=colors[nick]="3" sum % 6 + 1
		}
	}

	# Handle ACTION.
	if (index(type, "a")) {
		msg=nick " " msg
		nick="*"
	}

	printf "\n\033[%sm%s\033[0m \033[%sm%-10s\033[0m: %s",
		dcolor, date[2], ncolor, nick, msg
} END { print "" }
